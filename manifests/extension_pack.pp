# == Class: vbox::extensions
#
# Installs Oracle's VirtualBox Extension Pack.
#
# === Parameters
#
# Parameters other than `ensure` are for advanced usage only, you shouldn't
# have to customize these values.
#
# [*ensure*]
#  Whether or not to install the extension pack, defaults to 'present'
#  When set to 'absent', ensures removal of the extension pack.
#
# [*source*]
#  The URL of the extension pack.
#
# [*pack*]
#  The base name of the VirtualBox extension pack, for example:
#  "Oracle_VM_VirtualBox_Extension_Pack-4.3.28.vbox-extpack"
#
# [*directory*]
#  The directory created when the VirtualBox extension pack is installed,
#  default is platform-dependent.
#
# [*vboxmanage*]
#  The path to the VBoxManage command, defaults to '/usr/bin/VBoxManage'.
#
# [*license_agreement_hash*]
#  If specified, accepts the Oracle license agreement during installation
#  using the specified hash. A hash can be generated by running the
#  `VBoxManage extpack install` command manually and accepting the license
#  agreement.
#
class vbox::extension_pack(
  $ensure                 = 'present',
  $source                 = $vbox::params::extension_pack_url,
  $pack                   = $vbox::params::extension_pack,
  $directory              = $vbox::params::extension_pack_dir,
  $vboxmanage             = $vbox::params::vboxmanage,
  $license_agreement_hash = undef,
) inherits vbox::params {
  case $ensure {
    'installed', 'present': {
      include sys

      # Download the extension pack into root's home directory.
      # TODO: This needs to be saved to a platform-dependent cache directory
      #  and not in root's home.
      $pack_path = "${sys::root_home}/${pack}"
      sys::fetch { 'extension-pack':
        source      => $source,
        destination => $pack_path,
      }

      # Install the Extension Pack with `VBoxManage`.
      $escaped_version = inline_template(
        "<%= scope['vbox::params::version'].gsub('.', '\\.') %>"
      )
      if $license_agreement_hash {
        $license_args = "--accept-license=${license_agreement_hash}"
      }
      else {
        $license_args = ""
      }
      exec { 'extension_pack-install':
        command => "${vboxmanage} extpack install --replace ${pack} ${license_args}",
        path    => ['/bin', '/usr/bin'],
        user    => 'root',
        cwd     => $sys::root_home,
        unless  => "${vboxmanage} list extpacks | grep -e '^Version:[[:space:]]*${escaped_version}$'",
        require => [Sys::Fetch['extension-pack'], Class['vbox']],
      }
    }
    'absent': {
      exec { 'extension_pack-uninstall':
        command => "${vboxmanage} extpack uninstall 'Oracle VM VirtualBox Extension Pack'",
        path    => ['/bin', '/usr/bin'],
        user    => 'root',
        unless  => "test ! -d ${directory}",
        require => Class['vbox']
      }
    }
    default: {
      fail("Invalid ensure value for vbox::extension_pack: ${ensure}\n")
    }
  }
}
